apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'kotlin-android'

group = 'host.exp.exponent'
version = '8.3.1'

// Simple helper that allows the root project to override versions declared by this library.
def safeExtGet(prop, fallback) {
  rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

// Upload android library to maven with javadoc and android sources
configurations {
  deployerJars
}

// Creating sources with comments
task androidSourcesJar(type: Jar) {
  classifier = 'sources'
  from android.sourceSets.main.java.srcDirs
}

// Put the androidSources and javadoc to the artifacts
artifacts {
  archives androidSourcesJar
}

uploadArchives {
  repositories {
    mavenDeployer {
      configuration = configurations.deployerJars
      repository(url: mavenLocal().url)
    }
  }
}

android {
  compileSdkVersion safeExtGet("compileSdkVersion", 29)

  defaultConfig {
    minSdkVersion safeExtGet("minSdkVersion", 21)
    targetSdkVersion safeExtGet("targetSdkVersion", 28)
    versionCode 14
    versionName '8.3.1'
  }
  lintOptions {
    abortOnError false
  }
  compileOptions {
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
  }

  useLibrary 'android.test.runner'

  useLibrary 'android.test.base'
  useLibrary 'android.test.mock'
}

if (new File(rootProject.projectDir.parentFile, 'package.json').exists()) {
  apply from: project(':unimodules-core').file('../unimodules-core.gradle')
} else {
  throw new GradleException(
      '\'unimodules-core.gradle\' was not found in the usual Flutter or React Native dependency locations. ' +
          'This package can only be used in such projects. Are you sure you\'ve installed the dependencies properly?')
}

configurations.configureEach {
  if(it.getName().startsWith("test")) {
    resolutionStrategy.force project(":unimodules-test-core")
  }
}

dependencies {
  unimodule 'unimodules-core'

  testImplementation project(':unimodules-test-core')
  testImplementation 'org.robolectric:robolectric:4.3.1'

  api "androidx.browser:browser:1.2.0"
}
